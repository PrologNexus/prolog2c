#!/usr/bin/env qp
%%% run list of processes as "ensemble" -*- Prolog -*-


:- ensure_loaded(library(boss)).


main :-
	command_line_arguments(ARGS),
	process_arguments(ARGS),
	spawn_processes,
	process_requests.

process_arguments([]).
process_arguments(['-f', FILENAME|MORE]) :-
	processes_from_file(FILENAME),
	!,
	process_arguments(MORE).
process_arguments(['-v'|MORE]) :-
	recordz(boss_verbose, yes),
	!,
	process_arguments(MORE).
process_arguments(['-p', PROG|MORE]) :-
	recordz(subprocess, PROG),
	!,
	process_arguments(MORE).
process_arguments(['--'|REST]) :-
	recordz('arguments&1', arguments(REST)).
process_arguments([HELP|MORE]) :-
	memberchk(HELP, ['-h', '-help', '--help']),
	usage,
	halt.
process_arguments([OPT|_]) :-
	atom_concat('-', _, OPT),
	usage,
	halt(1).

usage :-
	display(user_error, 'usage: ensemble [-h] [-v] [-f FILENAME] [-p PROGRAM] [--] ...\n').

spawn_processes :-
	recorded(subprocess, PROG, REF),
	log_event("spawning program %s", [PROG]),
	spawn(PROG, [], _),
	erase(REF),
	fail.
spawn_processes.

processes_from_file(FILENAME) :-
	see(FILENAME),
	repeat,
	read_line(LINE),
	register_process(LINE),
	seen.

register_process(end_of_file).
register_process([35|_]) :- !, fail.
register_process([]) :- !, fail.
register_process(LINE) :-
	recordz(subprocess, LINE),
	fail.
